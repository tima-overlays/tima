/*
 * generated by Xtext 2.9.0
 */
package fr.labri.gossip.tima.generator

import com.google.inject.Inject
import fr.labri.Utils
import fr.labri.gossip.tima.dSL.Header
import fr.labri.gossip.tima.dSL.Message
import fr.labri.gossip.tima.semantic.DSLSemantic
import fr.labri.gossip.tima.semantic.MicroUtil
import fr.labri.gossip.tima.semantic.TimaAction
import fr.labri.gossip.tima.semantic.TimaAction.SimpleTimaAction
import fr.labri.gossip.tima.semantic.TimaGuard
import fr.labri.tima.CompiledTimedAutomata
import fr.labri.tima.DotRenderer
import fr.labri.tima.ITimedAutomata
import java.util.HashMap
import java.util.LinkedHashMap
import java.util.List
import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class DSLGenerator extends AbstractGenerator {

	@Inject
	extension DSLSemantic dslSemantic;

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		val s = resource.allContents.filter(Header).toList.get(0).name
		val automatons = resource.toTima
		fsa.generateFile('''«s»/«s»_real.dot''', '''
				«FOR a : automatons.entrySet SEPARATOR '\n' »
					«DotRenderer.toDot(a.value, a.key)»
				«ENDFOR»
				''')
		for (e : automatons.entrySet) {
			e.value = e.value.compile
		}
		fsa.generateFile('''«s»/«s»_compiled.dot''', '''
				«FOR a : automatons.entrySet SEPARATOR '\n' »
					«DotRenderer.toDot(a.value, a.key)»
				«ENDFOR»
				''')
		
		fsa.generateFile('''«s»/«s».cc''', native_version(s, automatons, resource.allContents.filter(Message).toList))
		fsa.generateFile('''«s»/«s».h''', native_version_header(s, automatons, resource.allContents.filter(Message).toList))
		fsa.generateFile('''«s»/«s»_inet.ned''', omnet_inet_app_descriptor(s, s + "_inet"))
		fsa.generateFile('''«s»/«s»_inet.h''', omnet_inet_app_header(s+"_inet"))
		fsa.generateFile('''«s»/«s»_inet.cc''', omnet_inet_app_code(s+"_inet"))
	}
	
	def omnet_inet_app_descriptor(String package_name, String project_name){
	'''
	package inet.applications.«package_name»;
	
	import inet.applications.contract.IUDPApp;
	
	//
	// This module is an application that implement the Gossip protocol
	// to disemminate messages.
	//
	// Some nodes distribute messages in the network using unicast UDP.
	//
	simple «project_name» like IUDPApp
	{
	    parameters:
	        
	        int destinationPort = default(10000);
	        int localPort = default(10000);
	        
	        string extra_options = default("");
	
	        string addresses = default(""); // network members
	          
		gates:
	        input udpIn @labels(UDPControlInfo/up);
	        output udpOut @labels(UDPControlInfo/down);
	}
	'''
	}
	
	def omnet_inet_app_code(String project_name) {
	'''
	#include "«project_name».h"
	#include "Tima_m.h"
	
	#include "inet/networklayer/common/L3AddressResolver.h"
	#include "inet/transportlayer/contract/udp/UDPControlInfo.h"
	
	#include <algorithm>
	
	namespace inet {
	
	Define_Module(«project_name»);
	
	
	void «project_name»::initialize(int stage)
	{
	    ApplicationBase::initialize(stage);
	
	    if (stage == INITSTAGE_LOCAL) {
	
	        localPort = par("localPort");
	        destinationPort = par("destinationPort");
	
	        const char *params = par("extra_options");
	        cStringTokenizer tokenizer(params,":");
	        const char *token;
	
	        while ((token = tokenizer.nextToken()) != nullptr) {
	            cStringTokenizer tokenizer2(token, "=");
	            const char* name = tokenizer2.nextToken();
	            const char* value= tokenizer2.nextToken();
	            options.emplace(name, value);
	        }
	
	
	        msg_tick = new cMessage("msg_ctrl", ControlMessageTypes::IDLE);
	    }
	}
	
	void «project_name»::handleMessageWhenUp(cMessage *msg)
	{
	    cPacket* pkt = nullptr;
	    tima::AbstractTimaNature::EventType eventType = tima::AbstractTimaNature::EventType::NONE;
	    if (msg->isSelfMessage()) {
	
	        switch (msg->getKind()) {
	            case ControlMessageTypes::START:
	                processStart();
	                break;
	            case ControlMessageTypes::TICK_MESSAGE:
	                eventType = tima::AbstractTimaNature::EventType::TICK;
	                break;
	        }
	    }
	    else if (msg->getKind() == UDP_I_DATA) {
	        eventType = tima::AbstractTimaNature::EventType::NETWORK_MESSAGE;
	    }
	
	    switch (eventType) {
	        case tima::AbstractTimaNature::EventType::TICK:
	            cancelAndDelete(msg);
	            executor->tick(msec);
	            configure_next_timer();
	            break;
	        case tima::AbstractTimaNature::EventType::NETWORK_MESSAGE:
	        {
	            pkt = PK(msg);
	
	            Tima* tima_msg = check_and_cast_nullable<Tima*>(dynamic_cast<Tima*>(pkt));
	            if (tima_msg != nullptr) {
	                executor->add_received_network_message(get_msg_id_from_name(tima_msg->getId()), (char*)tima_msg->getPayload());
	            }
	
	//            EV_DEBUG << "A network message: " << tima_msg->getId() << " " << tima_msg->getPayload() << '\n';
	
	            delete pkt;
	
	            break;
	        }
	    }
	}
	
	void «project_name»::finish()
	{
	    if (msg_tick)
	        cancelAndDelete(msg_tick);
	    msg_tick = nullptr;
	}
	
	bool «project_name»::handleNodeStart(IDoneCallback *doneCallback)
	{
	    msg_tick->setKind(START);
	    scheduleAt(simTime() + 0.01, msg_tick);
	    return true;
	}
	
	bool «project_name»::handleNodeShutdown(IDoneCallback *doneCallback)
	{
	    if (msg_tick)
	        cancelAndDelete(msg_tick);
	    msg_tick = nullptr;
	
	    return true;
	}
	
	void «project_name»::handleNodeCrash()
	{
	    if (msg_tick)
	        cancelAndDelete(msg_tick);
	    msg_tick = nullptr;
	}
	
	void «project_name»::configure_next_timer() {
	    msec = executor->global_deadline();
	//    EV_DEBUG << "next deadline is in " << msec << " milliseconds\n";
	    if (msec == tima::never_timeout) {
	        msec = 100;
	    }
	    if (msec > 30) {
	        msec = 30;
	    }
	    nature->configure_timer((uint64_t) ((msec)) * 1000000);
	    nature->schedule_events();
	}
	
	void «project_name»::processStart()
	{
	    myself = this->getParentModule()->getFullName();
	    L3AddressResolver().tryResolve(myself.c_str(), myAddress);
	    EV_TRACE << "Starting the process in module " << myself << " (" << myAddress.str() << ")" << "\n";
	
	
	
	    const char *destAddrs = par("addresses");
	    cStringTokenizer tokenizer(destAddrs);
	    const char *token;
	    std::vector<L3Address> possibleNeighbors;
	
	    while ((token = tokenizer.nextToken()) != nullptr) {
	        L3Address result;
	        L3AddressResolver().tryResolve(token, result);
	        if (result.isUnspecified())
	            EV_ERROR << "cannot resolve destination address: " << ((token)?token:"NULL") << endl;
	        else if (myself != token)
	            possibleNeighbors.push_back(result);
	    }
	
	    nature = std::make_shared<OMNetTimaNature>(myself, socket, possibleNeighbors, this);
	    nature->initialize();
	    nature->configure_communication(localPort);
	
	
	    EV_TRACE << "Creating protocol's executer\n";
	    executor = std::unique_ptr<tima::Executor>(new tima::Executor(nature, options));
	    configure_next_timer();
	}
	
	} //namespace
	'''
	}
	
	def omnet_inet_app_header(String project_name) {
	'''
	#ifndef __INET_«project_name.toUpperCase»_H_
	#define __INET_«project_name.toUpperCase»_H_
	
	#include <omnetpp.h>
	
	#include <map>
	#include <vector>
	#include <string>
	#include <memory>
	
	#include "inet/common/INETDefs.h"
	
	#include "inet/applications/base/ApplicationBase.h"
	#include "inet/transportlayer/contract/udp/UDPSocket.h"
	
	#include "Tima_m.h"
	
	#include "executor.h"
	#include "automata.h"
	#include "mailbox.h"
	#include "tima.h"
	
	#include "omnetpp_tima_nature.h"
	
	namespace inet {
	
	
	class INET_API «project_name» : public ApplicationBase
	{
	  protected:
	
	    int destinationPort = 10000;
	    int localPort = 10000;
	
	    // communication
	    UDPSocket socket;
	
	    // control messages
	    cMessage* msg_tick = nullptr;
	
	    // myself as a module
	    std::string myself;
	    L3Address myAddress;
	
	    int msec;
	
	    std::shared_ptr< tima::AbstractTimaNature > nature;
	    std::unique_ptr<tima::Executor> executor;
	
	    std::map<std::string, std::string> options;
	
	  protected:
	
	    virtual int numInitStages() const override { return NUM_INIT_STAGES; }
	    virtual void initialize(int stage) override;
	    virtual void handleMessageWhenUp(cMessage *msg) override;
	    virtual void finish() override;
	
	    virtual bool handleNodeStart(IDoneCallback *doneCallback) override;
	    virtual bool handleNodeShutdown(IDoneCallback *doneCallback) override;
	    virtual void handleNodeCrash() override;
	
	    virtual void processStart();
	
	private:
	    void configure_next_timer();
	};
	
	} //namespace
	
	#endif
	'''
	}
	
	def native_version_header(String name, LinkedHashMap<String, ITimedAutomata<String>> map, List<Message> messages) {
		val classes = new LinkedHashMap<String,HashMap<String, Boolean>>
		for (entry : map.entrySet) {
			val a = entry.value
			for (state : a.states) {
				// guards
				for (follower : a.getFollowers(state).filter[Utils.indexOf(it, a.states) != a.asCompiled.getTimeoutDestination(state)]) {
					val pred = a.getPredicate(state, follower).toString
					val members = pred.split("::")
					if (members.get(0) != "tima") {
						if (!classes.containsKey(members.get(0))) {
							classes.put(members.get(0), new HashMap<String, Boolean>)
						} 
						classes.get(members.get(0)).put(members.get(1), false)
					}
				}
				// actions
				for (act : state.actions) {
					for (aaa : (act as TimaAction<String>).pre_actions) {
						val members = aaa.type.split("::")
						if (members.get(0) != "tima") {
							if (!classes.containsKey(members.get(0))) {
								classes.put(members.get(0), new HashMap<String, Boolean>)
							} 
							classes.get(members.get(0)).put(members.get(1), true)
						}	
					}
					for (aaa : (act as TimaAction<String>).post_actions) {
						val members = aaa.type.split("::")
						if (members.get(0) != "tima") {
							if (!classes.containsKey(members.get(0))) {
								classes.put(members.get(0), new HashMap<String, Boolean>)
							} 
							classes.get(members.get(0)).put(members.get(1), true)
						}	
					}
					for (aaa : (act as TimaAction<String>).each_actions) {
						val members = aaa.type.split("::")
						if (members.get(0) != "tima") {
							if (!classes.containsKey(members.get(0))) {
								classes.put(members.get(0), new HashMap<String, Boolean>)
							} 
							classes.get(members.get(0)).put(members.get(1), true)
						}	
					}
				}
			}
		
		}
		'''
		#ifndef __«name»__
		#define __«name»__
		
		#include "automata.h"
		#include "tima.h"
		#include <string>
		
		int	get_msg_id_from_name(const char* name);
		
		/** ID for each automaton */
		enum AUTOMATA_ID {
			ANY_AUTOMATON_ID,
			«FOR a: map.entrySet SEPARATOR ','»
				«a.key»_AUTOMATON_ID
			«ENDFOR»
		};
		
		enum MESSAGES_ID {
			ANY_MSG_ID,
			«FOR m : messages SEPARATOR ','»
				«m.name»_MSG_ID
			«ENDFOR»
		};
		
		«FOR m : messages SEPARATOR '\n'»
			struct Message«m.name.toFirstUpper» : public tima::Message {
				Message«m.name.toFirstUpper»(): Message(«m.name»_MSG_ID, 0) {}
			};		
		«ENDFOR»
		
		«FOR clazz: classes.keySet SEPARATOR '\n'»
			class «clazz» {
			public:
				«FOR method: classes.get(clazz).entrySet»
				«IF !method.value»
				static bool «method.key»(std::string& name, tima::TimaNativeContext* context);
				«ELSE»
				static void «method.key»(std::string& name, tima::TimaNativeContext* context);
				«ENDIF»
				«ENDFOR»
			};
		«ENDFOR»
		
		#endif
		'''
	}
	
	def native_version(String project_name, LinkedHashMap<String, ITimedAutomata<String>> map, List<Message> messages) {
	'''
	#include "automata.h"
	#include "tima.h"
	#include "mailbox.h"
	#include "«project_name».h"
	#include <cstring>
	
	int
	get_msg_id_from_name(const char* name)
	{
		«FOR m : messages»
			if (!strcmp(name, "«m.name»"))
				return MESSAGES_ID::«m.name»_MSG_ID;
		«ENDFOR»
		return -1;
	}
	
	«FOR a: map.entrySet SEPARATOR '\n'»
	/** Automaton «a.key» */
	
	«FOR state: a.value.states SEPARATOR '\n'»
		«FOR act : state.actions»
		static void
		«a.key»_«state.name»_pre_action(std::string& name, tima::TimaNativeContext* ctx)
		{
			tima::TimaNativeContext* ctx2;
			«FOR act_simple : (act as TimaAction<String>).pre_actions»
				«actionStep(act_simple)»
			«ENDFOR»
		}
		static void
		«a.key»_«state.name»_post_action(std::string& name, tima::TimaNativeContext* ctx)
		{
			tima::TimaNativeContext* ctx2;
			«FOR act_simple : (act as TimaAction<String>).post_actions»
				«actionStep(act_simple)»
			«ENDFOR»
		}
		static void
		«a.key»_«state.name»_each_action(std::string& name, tima::TimaNativeContext* ctx)
		{
			tima::TimaNativeContext* ctx2;
			«FOR act_simple : (act as TimaAction<String>).each_actions»
				«actionStep(act_simple)»
			«ENDFOR»
		}
		«ENDFOR»
	«ENDFOR»
	
	«FOR state: a.value.states SEPARATOR '\n'»
		static struct tima::Transition «a.key»_«state.name»[] = {
		«FOR follower : a.value.getFollowers(state).filter[Utils.indexOf(it, a.value.states) != a.value.asCompiled.getTimeoutDestination(state)] SEPARATOR ','»
			{
				.dst = «Utils.indexOf(follower, a.value.states)»,
				.guard = «a.value.getPredicate(state, follower).type»,
				.msg_id = MESSAGES_ID::«(a.value.getPredicate(state, follower) as TimaGuard<?>).messageID»_MSG_ID,
				.src_id = AUTOMATA_ID::«(a.value.getPredicate(state, follower) as TimaGuard<?>).sourceID»_AUTOMATON_ID
			}
		«ENDFOR»
		};
	«ENDFOR»
	
	static struct tima::State «a.key»_states[] = {
	«FOR state: a.value.states SEPARATOR ','»
		{
		.name = "«state.name»",
		.urgent = «IF (MicroUtil.isUrgent(state))»true«ELSE»false«ENDIF»,
		.timeout = «IF (a.value.asCompiled).getStateTimeOut(state)==-1»tima::never_timeout«ELSE»«(a.value.asCompiled).getStateTimeOut(state)»«ENDIF», // milliseconds
		.timeout_destination = «IF (a.value.asCompiled).getTimeoutDestination(state)==-1»tima::null_destination«ELSE»«(a.value.asCompiled).getTimeoutDestination(state)»«ENDIF»,
		.nr_transitions = «numberOfFollowers(a.value, state)», // without taking into account the default transition
		.transitions = «a.key»_«state.name»,
		.pre_action = «a.key»_«state.name»_pre_action,
		.post_action = «a.key»_«state.name»_post_action,
		.each_action = «a.key»_«state.name»_each_action
		}
	«ENDFOR»
	};
	
	static struct tima::Automata «a.key» = {
		.name = "«a.key»",
		.initial = «Utils.indexOf(a.value.initialState, a.value.states)»,
		.nr_states = «a.value.states.length»,
		.states = «a.key»_states
	};
	
	«ENDFOR»
	
	static const uint32_t nr_automatas = «map.size»;
	
	static struct tima::Automata* automatons [] = {
		«FOR name : map.keySet SEPARATOR ','»
		&«name»
		«ENDFOR»
	};
	
	uint32_t
	get_nr_automatas()
	{
		return nr_automatas;
	}
	
	struct tima::Automata&
	get_automata(uint32_t idx)
	{
		return *automatons[idx];
	}
	
	'''
	}
	
	
	def actionStep(SimpleTimaAction<String> act_simple)
		'''«IF act_simple.isMessage»
		ctx2 = new tima::SendTimaContext(«act_simple.msg_id», «act_simple.src_id», "«act_simple.automaton_dst»", ctx->get_device_name(), ctx->get_user_data());
		«act_simple»(name, ctx2);
		free(ctx2);
		«ELSE»
		«act_simple»(name, ctx);
		«ENDIF»'''
	
	def numberOfFollowers(ITimedAutomata<String> a, ITimedAutomata.State s) {
		a.getFollowers(s).length - (if (a.asCompiled.getTimeoutDestination(s) != -1) 1 else 0)
	}
	
	def asCompiled(ITimedAutomata<String> a) {
		a as CompiledTimedAutomata<String>
	}
	
}
