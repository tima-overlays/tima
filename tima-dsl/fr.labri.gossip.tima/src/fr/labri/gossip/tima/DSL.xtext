grammar fr.labri.gossip.tima.DSL with org.eclipse.xtext.common.Terminals

generate dSL "http://www.labri.fr/gossip/tima/DSL"

Model:
	header=Header
	messages+=Message*
	automatons+=Automata*
;

Header:
	TK_MODULE name=ID
	Summary?
;

fragment
Summary : TK_SUMMARY summary=STRING;

Message:
	name=ID
	('(' Fields ')')?
	(':' FieldDeclaration)?
;

fragment
FieldDeclaration:
	declaredFields+=ID (',' declaredFields+=ID)*
;

Automata:
	TK_AUTOMATON name=ID
	Summary?
	states+=State* 
;

State:
	StateModifiers TK_STATE name=ID Actions? transitions+=Transition*
;

fragment
StateModifiers:
	urgent?='urgent'? & initial?='initial'?;

fragment
Actions:
	TK_ACTION actions+=Action+
;

Action:
	 builtinAction=BuiltinAction |
	 externalAction=ExternalAction |
	 msg=MessageAction
;

fragment
Fields:
	fields+=Field (',' fields+=Field)*
;

Field:
	name=ID ':' value=STRING
;

BuiltinAction:
	name=ID Fields?
;

ExternalAction:
	name=EXTERNAL_NAME
;

MessageAction:
	'!' type=[Message]
	('(' Fields? ')' | Fields)?
	'@' (broadcast='*' |address=STRING | target=[Automata])
;

Transition:
	TK_GUARD guards=Guard '=>' target=[State]
	Actions?
;

Guard:
	(	msg=MessageGuard|
		builtinGuard=BuiltinAction |
		externalGuard=ExternalAction)
		('within' value=INT unit=TimeUnit)?
	| 'timeout' {Guard} //value=INT unit=Unit
;

MessageGuard:
	'?'
	('_' {Wildcard}
	| msg=MessagePattern)
;

MessagePattern:
	type=[Message]
	('(' patterns+=PatternExpression ')')?
;

PatternExpression:
	operands+=Expression Operator operands+=Expression |
	Operator '(' Expressions ')'
;

fragment
Operator: operator=(ID|'='|'<>'|'>='|'<='|'<'|'>');

fragment
Expressions:
	operands+=Expression (',' operands+=Expression)*
;

Expression:
	field=ID
	| value=STRING
;

fragment
Patterns: patterns+=Pattern (',' patterns+=Pattern)* ;

Pattern:
	name=ID '=' value=STRING
	| name=ID 'likes' value=STRING
	| op=ID '('  ')' 
;


enum TimeUnit: MSEC='msec' | SEC='sec';

terminal TK_AUTOMATON: 'automaton';
terminal TK_STATE: 'state';
terminal TK_ACTION: 'do';
terminal TK_GUARD: 'when';
terminal TK_MODULE: 'module';
terminal TK_SUMMARY: 'summary';

terminal EXTERNAL_NAME:  '`' ( '\\' . | !('\\'|'`') )* '`';