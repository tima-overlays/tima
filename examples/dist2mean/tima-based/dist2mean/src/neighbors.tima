module neighbors

protocol neighbors { mode = "udp", port = 10000 } 

local messages {
	wakeup { sender }
} 

finding_neighbors messages {
	/* this one is a trick to find your neighbors  */
	hello { sender, x, y }  
}

extern int compute_distance(int x1, int y1, int x2, int y2);

/* automaton used to build set of neighbors */ 
template automaton FindingNeighbors (NextPhase)
	
	initial state greeting
		when ? hello within 100ms => greeting do {
			$distance[hello.sender] = compute_distance($posX, $posY, hello.x, hello.y)
			$coordinates[hello.sender].x = hello.x
			$coordinates[hello.sender].y = hello.y
		}
	
		when timeout => s_check do {
			$remaining_hellos = $remaining_hellos - 1
			! hello (x = $posX, y = $posY) @ *
		}

	urgent state s_check
		when $remaining_hellos == 0 => s0 
		when $remaining_hellos != 0 => greeting

	state s0
	
		when ? hello  within 100ms => s0 do { 
			$distance[hello.sender] = compute_distance($posX, $posY, hello.x, hello.y)
			$coordinates[hello.sender].x = hello.x
			$coordinates[hello.sender].y = hello.y
		} 
	
		when false within 5s => s0
		when timeout => done do	
			! wakeup @ NextPhase 
	
	final state done